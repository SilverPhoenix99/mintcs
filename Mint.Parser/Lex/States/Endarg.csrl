%%{
# newline significant, +/- is an operator., and unbound braces.

machine Lexer;

include "definitions.csrl";

EXPR_ENDARG := |*

    nl ws* ('&.' | '.' ^'.') => {
        fexec te - 2;
    };

    nl => {
        Lexer.EmitToken(kNL, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CommandStart = true;
        fbreak;
    };

    ':' ^':' => {
        Lexer.EmitToken(kCOLON, ts, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    '{' => {
        TokenType tokenType;
        if(Lexer.LeftParenCounter > 0 && Lexer.LeftParenCounter == Lexer.ParenNest)
        {
            tokenType = kLAMBEG;
            Lexer.LeftParenCounter = 0;
            Lexer.ParenNest--;
        }
        else
        {
            tokenType = kLBRACE_ARG;
            Lexer.CommandStart = true;
        }
        Lexer.EmitToken(tokenType, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.IncrementBraceCount();
        Lexer.Cond.Push(false);
        Lexer.Cmdarg.Push(false);
        fbreak;
    };

    '?' => {
        Lexer.EmitToken(kQMARK, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        fbreak;
    };

    (name [!?] - reserved) ^'=' => {
        Lexer.EmitToken(tFID, ts, te - 1);
        Lexer.CurrentState = Lexer.EndState;
        fhold;
        fbreak;
    };

    constant - reserved => {
        Lexer.EmitToken(tCONSTANT, ts, te);
        Lexer.CurrentState = Lexer.EndState;
        fbreak;
    };

    identifier - reserved => {
        var token = Lexer.EmitToken(tIDENTIFIER, ts, te);
        Lexer.CurrentState = Lexer.EndState;
        var isLocalVar = Lexer.IsVariableDefined(token.Value);
        if(isLocalVar)
        {
            Lexer.CanLabel = true;
        }
        fbreak;
    };

    'do' => {
        var tokenType = kDO_BLOCK;

        if(Lexer.LeftParenCounter > 0 && Lexer.LeftParenCounter == Lexer.ParenNest)
        {
            Lexer.LeftParenCounter = 0;
            Lexer.ParenNest--;
            tokenType = kDO_LAMBDA;
        }
        else if(Lexer.Cond.Peek)
        {
            tokenType = kDO_COND;
        }

        Lexer.EmitToken(tokenType, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CommandStart = true;
        fbreak;
    };

    any => CallSharedState;

*|;

}%%
#pragma warning disable 162

using Mint.Parse;
using static Mint.Parse.TokenType;

namespace Mint.Lex.States
{
    internal class Endarg : StateBase
    {
        public Endarg(Lexer lexer) : base(lexer)
        { }

        public override void Advance(State caller)
        {
            Reset(%%{ write start; }%%);

            %% write exec noend;
        }

        %% write data nofinal noerror;
    }
}