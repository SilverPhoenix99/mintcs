%%{
# newline significant, +/- is an operator.  just after a label.

machine Lexer;

include "definitions.csrl";

EXPR_ARG_LABELED := |*

    nl => {
        if(Lexer.InKwarg)
        {
            Lexer.EmitToken(kNL, ts, te);
            Lexer.CurrentState = Lexer.BegState;
            Lexer.CommandStart = true;
            fbreak;
        }
    };

    esc_ws_nl+ '**' ^(ws_nl | '=') => {
        Lexer.EmitToken(kDSTAR, te - 3, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    '**' ^'=' => {
        Lexer.EmitToken(kDSTAR, ts, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    esc_ws_nl+ '*' ^(ws_nl | '=') => {
        Lexer.EmitToken(kSTAR, te - 2, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    '*' ^'=' => {
        Lexer.EmitToken(kSTAR, ts, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    heredoc_identifier => {
        Lexer.EmitHeredocToken(ts, te);
        fbreak;
    };

    [''""] => {
        Lexer.EmitLabelableStringToken(ts, te);
        fbreak;
    };

    esc_ws_nl+ '&' ^(ws_nl | [=&.]) => {
        Lexer.EmitToken(kAMPER, te - 2, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    '&' ^[=&.] => {
        Lexer.EmitToken(kAMPER, ts, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    esc_ws_nl+ %TokStart '+' integer_number => {
        Lexer.EmitIntegerToken(tokStart, te, numBase, isRational, isImaginary);
        Lexer.CurrentState = Lexer.BegState;
        fbreak;
    };

    esc_ws_nl+ %TokStart '+' float_number => {
        Lexer.EmitFloatToken(tokStart, te, isRational, isImaginary);
        Lexer.CurrentState = Lexer.BegState;
        fbreak;
    };

    esc_ws_nl+ '+' ^(ws_nl | '=') => {
        Lexer.EmitToken(kUPLUS, te - 2, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    '+' integer_number => {
        Lexer.EmitIntegerToken(ts, te, numBase, isRational, isImaginary);
        Lexer.CurrentState = Lexer.BegState;
        fbreak;
    };

    '+' float_number => {
        Lexer.EmitFloatToken(ts, te, isRational, isImaginary);
        Lexer.CurrentState = Lexer.BegState;
        fbreak;
    };

    '+' ^'=' => {
        Lexer.EmitToken(kUPLUS, ts, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    esc_ws_nl+ '-' digit => {
        Lexer.EmitToken(kUMINUS_NUM, te - 2, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    esc_ws_nl+ '-' ^(ws_nl | digit | [=>]) => {
        Lexer.EmitToken(kUMINUS, te - 2, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    '-' digit => {
        Lexer.EmitToken(kUMINUS_NUM, ts, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    '-' ^(ws_nl | [=>]) => {
        Lexer.EmitToken(kUMINUS, ts, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    esc_ws_nl+ '::' => {
        Lexer.EmitToken(kCOLON3, te - 2, te);
        Lexer.CurrentState = Lexer.BegState;
        fbreak;
    };

    '::' => {
        Lexer.EmitToken(kCOLON3, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        fbreak;
    };

    esc_ws_nl+ '/' ^(ws_nl | '=') => {
        Lexer.EmitStringToken(te - 2, te - 1);
        fhold;
        fbreak;
    };

    '/' => {
        Lexer.EmitStringToken(ts, te);
        fbreak;
    };

    esc_ws_nl+ '(' => {
        Lexer.EmitToken(kLPAREN_ARG, te - 1, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CanLabel = true;
        Lexer.ParenNest++;
        Lexer.Cond.Push(false);
        Lexer.Cmdarg.Push(false);
        fbreak;
    };

    '(' => {
        Lexer.EmitToken(kLPAREN, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CanLabel = true;
        Lexer.ParenNest++;
        Lexer.Cond.Push(false);
        Lexer.Cmdarg.Push(false);
        fbreak;
    };

    esc_ws_nl+ '[' => {
        Lexer.EmitToken(kLBRACK, te - 1, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CanLabel = true;
        Lexer.ParenNest++;
        Lexer.Cond.Push(false);
        Lexer.Cmdarg.Push(false);
        fbreak;
    };

    '[' => {
        Lexer.EmitToken(kLBRACK, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CanLabel = true;
        Lexer.ParenNest++;
        Lexer.Cond.Push(false);
        Lexer.Cmdarg.Push(false);
        fbreak;
    };

    '%' [Qqxr] (ascii - alnum) => {
        Lexer.EmitStringToken(ts, te);
        fbreak;
    };

    '%s' (ascii - alnum) => {
        Lexer.EmitStringToken(ts, te);
        Lexer.CurrentState = Lexer.FnameFitemState;
        fbreak;
    };

    '%' [WwIi] (ascii - alnum) ws_nl* => {
        Lexer.EmitStringToken(ts, ts + 3);
        fbreak;
    };

    '%' (ascii - alnum) => {
        Lexer.EmitStringToken(ts, te);
        fbreak;
    };

    '%' [QqWwIixrs]? c_eof => {
        var line = Lexer.LocationFor(ts, 0).StartLine;
        throw new SyntaxError(Lexer.Filename, line, "unterminated quoted string meets end of file");
    };

    '%' [QqWwIixrs]? (unicode | alnum) => {
        var line = Lexer.LocationFor(ts, 0).StartLine;
        throw new SyntaxError(Lexer.Filename, line, "unknown type of %string");
    };

    '?' ws_nl => {
        Lexer.EmitToken(kQMARK, ts, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    'if' => {
        Lexer.EmitToken(kIF, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CommandStart = true;
        fbreak;
    };

    'unless' => {
        Lexer.EmitToken(kUNLESS, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CommandStart = true;
        fbreak;
    };

    'until' => {
        Lexer.EmitToken(kUNTIL, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CommandStart = true;
        fbreak;
    };

    'while' => {
        Lexer.EmitToken(kWHILE, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CommandStart = true;
        fbreak;
    };

    'rescue' => {
        Lexer.EmitToken(kRESCUE, ts, te);
        Lexer.CurrentState = Lexer.MidState;
        fbreak;
    };

    'do' => {
        var tokenType = kDO;

        if(Lexer.LeftParenCounter > 0 && Lexer.LeftParenCounter == Lexer.ParenNest)
        {
            Lexer.LeftParenCounter = 0;
            Lexer.ParenNest--;
            tokenType = kDO_LAMBDA;
        }
        else if(Lexer.Cond.Peek)
        {
            tokenType = kDO_COND;
        }
        else if(Lexer.Cmdarg.Peek)
        {
            tokenType = kDO_BLOCK;
        }

        Lexer.EmitToken(tokenType, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CommandStart = true;
        fbreak;
    };

    any => CallSharedState;

*|;

}%%
#pragma warning disable 162

using Mint.Parse;
using static Mint.Parse.TokenType;

namespace Mint.Lex.States
{
    internal class ArgLabeled : StateBase
    {
        protected override int InitialState => %%{ write start; }%%;

        public ArgLabeled(Lexer lexer) : base(lexer)
        { }

        public override void EmitIdentifierToken(int ts, int te)
        {
            var token = Lexer.EmitToken(tIDENTIFIER, ts, te);
            var isLocalVar = Lexer.IsVariableDefined(token.Value);
            if(isLocalVar)
            {
                Lexer.CurrentState = Lexer.EndState;
                Lexer.CanLabel = true;
            }
            else
            {
                Lexer.CurrentState = Lexer.CommandStart ? Lexer.CmdargState : Lexer.ArgState;
            }
        }

        public override void EmitFidToken(int ts, int te)
        {
            Lexer.EmitToken(tFID, ts, te);
            Lexer.CurrentState = Lexer.CommandStart ? Lexer.CmdargState : Lexer.ArgState;
        }

        protected override void InternalAdvance()
        {
            %% write exec noend;
        }

        %% write data nofinal noerror;
    }
}