%%{
# newline significant, +/- is an operator. can label.

machine Lexer;

include "definitions.csrl";

main := |*

    nl ws* ('&.' | '.' ^'.') => {
        fexec te - 2;
    };

    nl => {
        Lexer.EmitToken(kNL, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CommandStart = true;
        fbreak;
    };

    esc_ws_nl+ '**' ^(ws_nl | '=') => {
        Lexer.EmitToken(kDSTAR, te - 3, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    esc_ws_nl+ '*' ^(ws_nl | '=') => {
        Lexer.EmitToken(kSTAR, te - 2, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    esc_ws_nl+ %TokStart heredoc_identifier => {
        Lexer.EmitHeredocToken(tokStart, te);
        fbreak;
    };

    [''""] => {
        Lexer.EmitLabelableStringToken(ts, te);
        fbreak;
    };

    esc_ws_nl+ '&' ^(ws_nl | [=&.]) => {
        Lexer.EmitToken(kAMPER, te - 2, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    esc_ws_nl+ %TokStart '+' integer_number => {
        Lexer.EmitIntegerToken(tokStart, te, numBase, isRational, isImaginary);
        Lexer.CurrentState = Lexer.BegState;
        fbreak;
    };

    esc_ws_nl+ %TokStart '+' float_number => {
        Lexer.EmitFloatToken(tokStart, te, isRational, isImaginary);
        Lexer.CurrentState = Lexer.BegState;
        fbreak;
    };

    esc_ws_nl+ '+' ^(ws_nl | '=') => {
        Lexer.EmitToken(kUPLUS, te - 2, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    esc_ws_nl+ '-' digit => {
        Lexer.EmitToken(kUMINUS_NUM, te - 2, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    esc_ws_nl+ '-' ^(ws_nl | digit | [=>]) => {
        Lexer.EmitToken(kUMINUS, te - 2, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    esc_ws_nl+ '::' => {
        Lexer.EmitToken(kCOLON3, te - 2, te);
        Lexer.CurrentState = Lexer.BegState;
        fbreak;
    };

    esc_ws_nl+ '/' ^(ws_nl | '=') => {
        Lexer.EmitStringToken(te - 2, te - 1);
        fhold;
        fbreak;
    };

    esc_ws_nl+ '(' => {
        Lexer.EmitToken(kLPAREN_ARG, te - 1, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CanLabel = true;
        Lexer.ParenNest++;
        Lexer.Cond.Push(false);
        Lexer.Cmdarg.Push(false);
        fbreak;
    };

    esc_ws_nl+ '[' => {
        Lexer.EmitToken(kLBRACK, te - 1, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CanLabel = true;
        Lexer.ParenNest++;
        Lexer.Cond.Push(false);
        Lexer.Cmdarg.Push(false);
        fbreak;
    };

    esc_ws_nl+ '%' [Qqxr] (ascii - alnum) => {
        Lexer.EmitStringToken(te - 3, te);
        fbreak;
    };

    esc_ws_nl+ '%s' (ascii - alnum) => {
        Lexer.EmitStringToken(te - 3, te);
        Lexer.CurrentState = Lexer.FnameFitemState;
        fbreak;
    };

    esc_ws_nl+ %TokStart '%' [WwIi] (ascii - alnum) ws_nl* => {
        Lexer.EmitStringToken(tokStart, tokStart + 3);
        fbreak;
    };

    esc_ws_nl+ '%' (ascii - alnum - ws_nl - '=') => {
        Lexer.EmitStringToken(te - 2, te);
        fbreak;
    };

    esc_ws_nl+ '%' [QqWwIixrs]? c_eof => {
        throw new SyntaxError(Lexer.Filename, Lexer.LineAt(ts), "unterminated quoted string meets end of file");
    };

    esc_ws_nl+ '%' [QqWwIixrs]? (unicode | alnum) => {
        throw new SyntaxError(Lexer.Filename, Lexer.LineAt(ts), "unknown type of %string");
    };

    '?' ws_nl => {
        Lexer.EmitToken(kQMARK, ts, te - 1);
        Lexer.CurrentState = Lexer.BegState;
        fhold;
        fbreak;
    };

    any => CallBaseState;

*|;

}%%
#pragma warning disable 162

using static Mint.Parse.TokenType;

namespace Mint.Lex.States
{
    internal partial class Arg
    {
        public override void Advance()
        {
            Reset(%%{ write start; }%%);

            %% write exec noend;
        }

        %% write data nofinal noerror;
    }
}