%%{
# newline significant, +/- is an operator. can label.

machine Lexer;

include "definitions.csrl";

main := |*

    nl ws* ('&.' | '.' ^'.') => {
        fexec te - 2;
    };

    nl => {
        Lexer.EmitToken(kNL, ts, te);
        Lexer.CurrentState = Lexer.BegState;
        Lexer.CommandStart = true;
        fbreak;
    };

    esc_ws_nl+ %TokStart heredoc_identifier => {
        Lexer.EmitHeredocToken(tokStart, te);
        fexec Lexer.NextLinePosition();
        fbreak;
    };

    esc_ws_nl+ '%' [QqWwIixrs]? %eof(UnterminatedQuotedString);

    esc_ws_nl+ '%' [Qqxr] percent_string_delimiter => {
        Lexer.EmitStringBeginToken(te - 3, te);
        fbreak;
    };

    esc_ws_nl+ '%s' percent_string_delimiter => {
        Lexer.EmitStringBeginToken(te - 3, te, endState: Lexer.FnameFitemState);
        fbreak;
    };

    esc_ws_nl+ %TokStart '%' [WwIi] percent_string_delimiter ws_nl* => {
        Lexer.EmitStringBeginToken(tokStart, tokStart + 3);
        fbreak;
    };

    esc_ws_nl+ '%' (percent_string_delimiter - ws_nl - '=') => {
        Lexer.EmitStringBeginToken(te - 2, te);
        fbreak;
    };

    esc_ws_nl+ '%' [QqWwIixrs]? (unicode | alnum) => {
        throw new SyntaxError(Lexer.Filename, Lexer.LineAt(ts), "unknown type of %string");
    };

    any => CallBaseState;

*|;

}%%
#pragma warning disable 162

using static Mint.Parse.TokenType;

namespace Mint.Lex.States
{
    internal partial class Arg
    {
        public override void Advance()
        {
            Reset(%%{ write start; }%%);

            %% write exec;
        }

        %% write data nofinal noerror;
    }
}