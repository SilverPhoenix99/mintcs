%%{

machine Lexer;

include "definitions.csrl";

action IsDelimiter { IsDelimiter }

action IsNested { delimiter.IsNested }
action CanLabel { delimiter.CanLabel }
action HasInterpolation { delimiter.HasInterpolation }
action IsWords { delimiter.IsWords }

action EofLiteralError
{
    throw new SyntaxError(Lexer.Filename, Lexer.LineAt(ts), "unterminated string meets end of file");
}

action NewLine
{
    if(Lexer.LineJump > Lexer.Position)
    {
        EmitContent(te);
        fexec te = Lexer.LineJump;
        contentStart = ts = Lexer.Position;
    }
}

action WordNewLine
{
	if(Lexer.LineJump > Lexer.Position)
    {
        fexec te = Lexer.LineJump;
    }
}

action LabelDelimiter
{
    //# only ', " and ` can be used for labels

    EmitContent(te - 3);
    EmitEndToken(te - 3, te - 1);
    Lexer.CanLabel = true;
    fhold;
    fbreak;
}

action Delimiter
{
    EmitContent(te - 1);

    if(delimiter.IsRegexp)
    {
        contentStart = te - 1;
        fgoto REGEXP_END;
    }

    EmitEndToken(te - 1, te);
    fbreak;
}

string_nl = '\n' @NewLine;
string_any = any | string_nl;
string_delimiter = string_any when IsDelimiter;
word_nl = '\n' @WordNewLine;
word_space = space | word_nl;
interpolation_escape = '\\' when HasInterpolation any+;


main := |*

    # only ', " and ` can be used for labels
	string_delimiter when !IsNested ':' when CanLabel ^':' => LabelDelimiter;

	string_delimiter when !IsNested => Delimiter;

    string_delimiter when IsNested => { delimiter.DecrementNesting(); };

    [\[{(<] => { delimiter.IncrementNesting(); };

	word_space when IsWords word_space* => {
		EmitContent(ts);
		EmitSpace(ts, ts + 1);
		fbreak;
	};

    # accepted escapes even without interpolation
	'\\' string_delimiter;
	'\\' '\r'? string_nl;
	'\\\\';

	interpolation_escape & (char_escape - '\\\n' - '\\\\');
    interpolation_escape & octal_escape;
    interpolation_escape & hex_escape;
    interpolation_escape & multi_unicode_escape;
    interpolation_escape & control_escape;

	'#' when HasInterpolation '{' => { EmitDBeg(); fbreak; };
	'#' when HasInterpolation class_variable => { EmitDVar(tCVAR); fbreak; };
	'#' when HasInterpolation instance_variable => { EmitDVar(tIVAR); fbreak; };
	'#' when HasInterpolation global_variable => { EmitDVar(tGVAR); fbreak; };
	'#' when HasInterpolation nth_ref_variable => { EmitDVar(tNTH_REF); fbreak; };
	'#' when HasInterpolation back_ref_variable => { EmitDVar(tBACK_REF); fbreak; };

	string_any;

    zlen %eof(EofLiteralError);

*|;

REGEXP_END := |*
    (
          'm' @ { regexpOptions |= Multiline; }
        | 'i' @ { regexpOptions |= IgnoreCase; }
        | 'x' @ { regexpOptions |= Extend; }
        | 'o' @ { regexpOptions |= Once; }
        # accept the last encoding option
        | 'n' @ { regexpOptions = (regexpOptions & ~EncodingMask) | Ascii8; }
        | 'e' @ { regexpOptions = (regexpOptions & ~EncodingMask) | EucJp; }
        | 's' @ { regexpOptions = (regexpOptions & ~EncodingMask) | Windows31J; }
        | 'u' @ { regexpOptions = (regexpOptions & ~EncodingMask) | Utf8; }
    )+;

    ( alpha - [eimnosux] )+ => {
        var line = Lexer.LineAt(ts);
        var text = Lexer.TextAt(ts, te);
        var options = text.Length == 1 ? "option" : "options";
        throw new SyntaxError(Lexer.Filename, line, $"unknown regexp {options} - {text}");
    };

    ^alpha => {
        BeginToken.Properties["regexp_options"] = regexpOptions;
        EmitEndToken(contentStart, te - 1);
        fhold;
        fbreak;
    };
*|;

}%%
#pragma warning disable 162

using static Mint.Lex.RegexpFlags;
using static Mint.Parse.TokenType;

namespace Mint.Lex.States
{
    internal partial class StringLiteral
    {
        public override void Advance()
        {
            Reset(%%{ write start; }%%);

            %% write exec;
        }

        %% write data nofinal noerror;
    }
}
